#version 450
#extension GL_ARB_separate_shader_objects : enable
#extension GL_ARB_shading_language_420pack : enable

// This shader performs downsampling on a texture,
// as taken from Call Of Duty method, presented at ACM Siggraph 2014.
// This particular method was customly designed to eliminate
// "pulsating artifacts and temporal stability issues".

// Remember to add bilinear minification filter for this texture!
// Remember to use a floating-point texture format (for HDR)!
// Remember to use edge clamping for this texture!

layout(local_size_x = 16, local_size_y = 16) in;

layout(binding = 0, rgba32f) uniform writeonly image2D writeColor;
layout(binding = 1) uniform sampler2D samplerColor;

layout(push_constant) uniform UBOUpsample
{
	vec2 resolution;
} ubo;

void main()
{
	vec2 uv = (gl_GlobalInvocationID.xy + vec2(0.5f)) / vec2(imageSize(writeColor).xy);

    vec2 texelSize = 1.0 / ubo.resolution;
    
    float x = texelSize.x;
    float y = texelSize.y;

    // Take 13 samples around current texel:
    // a - b - c
    // - j - k -
    // d - e - f
    // - l - m -
    // g - h - i
    // === ('e' is the current texel) ===
    vec3 a = texture(samplerColor, vec2(uv.x - 2*x, uv.y + 2*y)).rgb;
    vec3 b = texture(samplerColor, vec2(uv.x,       uv.y + 2*y)).rgb;
    vec3 c = texture(samplerColor, vec2(uv.x + 2*x, uv.y + 2*y)).rgb;

    vec3 d = texture(samplerColor, vec2(uv.x - 2*x, uv.y)).rgb;
    vec3 e = texture(samplerColor, vec2(uv.x,       uv.y)).rgb;
    vec3 f = texture(samplerColor, vec2(uv.x + 2*x, uv.y)).rgb;

    vec3 g = texture(samplerColor, vec2(uv.x - 2*x, uv.y - 2*y)).rgb;
    vec3 h = texture(samplerColor, vec2(uv.x,       uv.y - 2*y)).rgb;
    vec3 i = texture(samplerColor, vec2(uv.x + 2*x, uv.y - 2*y)).rgb;

    vec3 j = texture(samplerColor, vec2(uv.x - x, uv.y + y)).rgb;
    vec3 k = texture(samplerColor, vec2(uv.x + x, uv.y + y)).rgb;
    vec3 l = texture(samplerColor, vec2(uv.x - x, uv.y - y)).rgb;
    vec3 m = texture(samplerColor, vec2(uv.x + x, uv.y - y)).rgb;

    // Apply weighted distribution:
    // 0.5 + 0.125 + 0.125 + 0.125 + 0.125 = 1
    // a,b,d,e * 0.125
    // b,c,e,f * 0.125
    // d,e,g,h * 0.125
    // e,f,h,i * 0.125
    // j,k,l,m * 0.5
    // This shows 5 square areas that are being sampled. But some of them overlap,
    // so to have an energy preserving downsample we need to make some adjustments.
    // The weights are the distributed, so that the sum of j,k,l,m (e.g.)
    // contribute 0.5 to the final color output. The code below is written
    // to effectively yield this sum. We get:
    // 0.125*5 + 0.03125*4 + 0.0625*4 = 1
    vec3 color = e*0.125;
    color += (a+c+g+i)*0.03125;
    color += (b+d+f+h)*0.0625;
    color += (j+k+l+m)*0.125;

    imageStore(writeColor, ivec2(gl_GlobalInvocationID.xy), vec4(color, 1.0));
}