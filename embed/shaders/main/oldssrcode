#version 450
#extension GL_GOOGLE_include_directive : require

//--------------------Includes--------------------
#include "../shader_util.glsl"
#include "../shared_shading.glsl"

//--------------------Texture bindings--------------------
layout (binding = 0) uniform usampler2D packed_tex;
layout (binding = 1) uniform sampler2D depth_tex;

//--------------------In/Out locations--------------------
layout (location = 0) noperspective in vec2 inUV;
layout (location = 0) out vec4 outFragcolor;

//--------------------Uniforms--------------------
layout (std140, binding = 3) uniform UBOReflections
{
	mat4 invViewProjection;
	mat4 projection;
    mat4 invView;
	vec4 viewPos;
} ubo;

const float rayStep = 0.25;
const float minRayStep = 0.1;
const float maxSteps = 20;
const float searchDist = 5;
const float searchDistInv = 0.2;
const int numBinarySearchSteps = 5;
const float maxDDepth = 1.0;
const float maxDDepthInv = 1.0;
const float reflectionSpecularFalloffExponent = 3.0;

vec3 binarySearch(vec3 dir, inout vec3 hitCoord, out float dDepth)
{
    float depth;
    for(int i = 0; i < numBinarySearchSteps; i++)
    {
        vec4 projectedCoord = ubo.projection * vec4(hitCoord, 1.0);
        projectedCoord.xy /= projectedCoord.w;
        projectedCoord.xy = projectedCoord.xy * 0.5 + 0.5;

        depth = texture(depth_tex, projectedCoord.xy).r;
        dDepth = hitCoord.z - depth;

        if(dDepth > 0.0)
            hitCoord += dir;

        dir *= 0.5;
        hitCoord -= dir;    
    }

    vec4 projectedCoord = ubo.projection * vec4(hitCoord, 1.0); 
    projectedCoord.xy /= projectedCoord.w;
    projectedCoord.xy = projectedCoord.xy * 0.5 + 0.5;

    return vec3(projectedCoord.xy, depth);
}

vec4 rayCast(vec3 dir, inout vec3 hitCoord, out float dDepth)
{
	float depth;
    dir *= rayStep;

    for(int i = 0; i < maxSteps; i++)
    {
        hitCoord += dir;

        vec4 projectedCoord = ubo.projection * vec4(hitCoord, 1.0);
        projectedCoord.xy /= projectedCoord.w;
        projectedCoord.xy = projectedCoord.xy * 0.5 + 0.5;

        depth = texture(depth_tex, projectedCoord.xy).r;
        dDepth = hitCoord.z - depth;

        if(dDepth < 0.0)
            return vec4(binarySearch(dir, hitCoord, dDepth), 1.0);
    }

    return vec4(0.0, 0.0, 0.0, 0.0);
}

void main()
{
    //Load depth and world position
	float depth = texture(depth_tex, inUV).r;
	vec3 inWorldPos = getPositionFromDepth(inUV, depth, ubo.invViewProjection);

    vec3 normal = vec3(0.0);
	vec3 albedo = vec3(0.0);
	vec4 mrah = vec4(0.0);

	// Loading texture pack
	uvec4 packed_data = texture(packed_tex, inUV);
	unpackTextures(packed_data, normal, albedo, mrah);

    float metalic = mrah.r;
	float roughness = mrah.g;
	float occlusion = mrah.b;
	float height = mrah.a;

    vec3 cameraPos = ubo.viewPos.xyz;
    //vec3 cameraPos = vec3(ubo.invView * vec4(inWorldPos, 1.0));
	// Calculate direction from fragment to viewPosition
	vec3 V = normalize(cameraPos - inWorldPos);
	// Reflection vector
	vec3 R = reflect(-V, normal);

    vec3 F0 = vec3(0.04f); 
	// Reflectance at normal incidence angle
	F0 = mix(F0, albedo, metalic);

    vec3 F = F_SchlickR(max(dot(normal, V), 0.0f), F0, roughness);

    // Ray cast
    vec3 hitPos = V;
    float dDepth;
    vec4 coords = rayCast(R * max(minRayStep, -V.z), hitPos, dDepth);
    vec2 dCoords = abs(vec2(0.5, 0.5) - coords.xy);
    float screenEdgefactor = clamp(1.0 - (dCoords.x + dCoords.y), 0.0, 1.0);
    float reflectionMultiplier = pow(metalic, reflectionSpecularFalloffExponent) * screenEdgefactor * -R.z;

    uvec4 packed_data_2 = texture(packed_tex, coords.xy);
	unpackTextures(packed_data_2, normal, albedo, mrah);
    vec3 reflection = albedo * clamp(reflectionMultiplier, 0.0, 0.9) * F; 

    outFragcolor = vec4(reflection, 1.0);
}